plugins {
    id "com.github.johnrengelman.shadow" version "5.0.0"
}

configurations {
    shadow
    compile.extendsFrom(shadow)
}

architectury {
    platformSetupLoomIde()
    fabric()
}
minecraft {
    accessWidener = file("src/main/resources/otyacraftengine.accesswidener")
}
repositories {
    maven { url "https://maven.terraformersmc.com/releases/" }
}
dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    modApi "me.shedaniel:architectury:${rootProject.architectury_version}:fabric"
    modApi "com.terraformersmc:modmenu:${modmenu_version}"

    modApi include("me.shedaniel.cloth:cloth-config:${rootProject.cloth_config_version}:fabric") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    implementation(project(path: ":common")) {
        transitive = false
    }
    developmentFabric(project(path: ":common")) {
        transitive = false
    }
    shadow(project(path: ":common", configuration: "transformProductionFabric")) {
        transitive = false
    }

    shadow "de.javagl:obj:0.3.0"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadow]
    classifier "dev-shadow"
    relocate 'de.javagl', 'red.felnull.otyacraftengine.libs.de.javagl'
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier "fabric"
}

jar {
    classifier "dev"
}

java {
    withSourcesJar()
}

sourcesJar {
    def commonSources = project(":common").remapSourcesJar
    dependsOn commonSources
    from zipTree(commonSources.output)
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            artifact(remapJar) {
                classifier null
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {
    }
}
